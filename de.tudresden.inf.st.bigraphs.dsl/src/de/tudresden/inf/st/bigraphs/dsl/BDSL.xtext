grammar de.tudresden.inf.st.bigraphs.dsl.BDSL with org.eclipse.xtext.common.Terminals

import "platform:/resource/BigraphBaseModel/model/bigraphBaseModel.ecore" as bigraph
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate bDSL "http://www.tudresden.de/inf/st/bigraphs/dsl/BDSL"

// types are in italic font
BRSModel:
	signature+=(Signature)*
	// unordered group
	((statements+=(AbstractElement)*) & (main=MainElement))

	//brs etc.
;

BRSModelImport:
	'import' importedNamespace=FqnWithWildCard;

FQN:
	ID ('.' ID)*; // TODO: could be now also IdOrKw instead of ID

FqnWithWildCard:
	FQN ('.*')?;

BigraphVarReferenceID hidden():
	('$' FQN); 
	

AbstractElement:
	LocalVarDecl | LocalRuleDecl | LocalPredicateDeclaration
	// and rules
;

MainElement:
	MainKeyword {MainElement} value+=(AbstractMainStatements)* '}';

MainKeyword:
	"main" "=" "{"
;

IdOrKw :
	ID | KEYWORD
;

KEYWORD :
	MainKeyword
	| "react"
	| "signature"
;

// Creates a signature class with the same name: shortcut for "signature returns signature"
Signature:
	{Signature}
	'signature'
	name=ID
	('{' (controls+=ControlDef)+ '}')?;

ControlDef returns ControlVariable:
	{ControlVariable} type=ControlType? 'ctrl' name=ID 'arity' arity=INT;



NameConstant:
	{NameConstant} name=STRING;

	//TODO: change to linkNamevars or so
SiteVars:
	name=ID;

Site:
	'id' '(' index=INT ')';
	
LVD2:
	{LocalVarDecl} "val" name=ID ':' type=[ControlVariable|FQN] '=' "{" definition+=(BigraphExpression)* "}";

LocalVarDecl:
	"val" {LocalVarDecl} (name=ID ("(" sig=[Signature] ")")? (("[" siteArgs+=SiteVars (',' siteArgs+=SiteVars)* "]")? ':'
	type=[ControlVariable|FQN])?
	("="
	"{"
	definition+=(BigraphExpression)*
	"}")
);

LocalRuleDecl:
	"react" {LocalRuleDecl} name=ID ("(" sig=[Signature] ")")?
	("="
	"{"
	redex=BigraphExpression
	"}"
	','
	'{'
	reactum=BigraphExpression
	'}');
	
LocalPredicateDeclaration:
	'pred' {LocalPredicateDeclaration} name=ID ("(" sig=[Signature] ")")? (':' type=PredicateType)? '=' '{'
	definition=BigraphExpression
	'}'
;

ElementaryBigraphs:
	Barren | Merge | Closure | Substitution
;

Barren:
	'barren' {Barren} '()'
;

Merge:
	'merge' {Merge} '(' sites=INT ')'
;

Closure:
	('closure' | 'clsre' | '/') {Closure} '(' value+=STRING (',' value+=STRING)* ')'
;

Substitution:
	('substitution' | 'subst') {Substitution} '(' from=STRING ',' ('[' to+=STRING (',' to+=STRING)* ']') ')'
;


BigraphVarReference:
	value=[LocalVarDecl|BigraphVarReferenceID];

RuleVarReference:
	value=[LocalRuleDecl|BigraphVarReferenceID];

PredicateVarReference:
	value=[LocalPredicateDeclaration|BigraphVarReferenceID]
;	

BRSVarReference:
	value=[BRSDefinition|BigraphVarReferenceID]
;
	


AbstractBigraphDeclaration:
	BigraphVarReference | NodeExpressionCall | LVD2 | Site | ElementaryBigraphs;

NodeExpressionCall:
	value=[ControlVariable|FQN] ("[" links+=NameConstant? (',' links+=NameConstant)* "]")?
;

BigraphExpression:
	Addition;

Addition returns BigraphExpression:
	Multiplication (({Plus.left=current} operator=BinaryParallelOperator) right=Multiplication)*;

Multiplication returns BigraphExpression:
	PrimaryExpression (({Multi.left=current} operator=BinaryNestingOperator) right=PrimaryExpression)*;

PrimaryExpression returns BigraphExpression:
	'(' BigraphExpression ')' |
	//	{BigraphLiteral} value=AbstractBigraphDeclaration
	AbstractBigraphDeclaration;

	//terminal CHAR: ('a'..'z'|'A'..'Z'|'0'..'9' | 'å' | 'ä' | 'ö');
//terminal BASICID: ('a'..'z'|'A'..'Z'|'0'..'9' | 'å' | 'ä' | 'ö')*;
//terminal STRING: '"' -> '"';
//terminal WS: (' '|'\t')+;
//terminal ANY_OTHER: .;m

AbstractMainStatements:
	BRSDefinition | MethodStatements;

MethodStatements:
	PrintLn | ExportMethod
;

BRSDefinition:
	{BRSDefinition} 'brs' name=ID '{'
	('agents' '=' ("[" agents+=BigraphVarReference? (',' agents+=BigraphVarReference)* "]"))?
	(',' 'rules' '=' ("[" rules+=RuleVarReference? (',' agents+=RuleVarReference)* "]"))?
	(',' 'preds' '=' ("[" predicates+=PredicateVarReference? (',' predicates+=PredicateVarReference)* "]"))?
	'}';

PrintLn:
	'println' {PrintLn} '(' text=(PrintableExpression) (',' mode=OutputModeModel)? ')';

PrintableExpression:
	({StringLiteral} value=STRING) | (BigraphVarReference);
	
ExportMethod:
	'export' {ExportMethod} '(' ((brs=BRSVarReference)) (',' 'as' '=' format=ExportFormat)? (',' 'to' '=' sink=ExportSink (',' 'filename' '=' filename=STRING)?)? ')'
;

enum BinaryParallelOperator:
	UNSET | MERGE='|' | PARALLEL='||';

enum BinaryNestingOperator:
	UNSET2 | NESTING='-' | COMPOSITION='*' //we cannot use a simple '.' right now
;

enum ControlType:
	ATOMIC='atomic' | ACTIVE='active' | PASSIVE='passive';
	
enum PredicateType:
	PARTIAL='partial' | ISO='iso' 
;

enum ExportFormat:
	XMI='xmi' | ECORE='ecore' | BIGRAPHER='bigrapher' | BIGMC='bigmc'
;

enum ExportSink:
	STDOUT='console' | FILE='file'
;

enum OutputModeModel:
	INSTANCE_MODEL='xmi' | META_MODEL='ecore';
